;set this to zero to create the vanilla_unlock patch
ALTERNATE_UNLOCK_CONTENT_METHOD EQU 1




IF ALTERNATE_UNLOCK_CONTENT_METHOD == 1

;UNLOCK MOBILE CONTENT
_SRAM_UNLOCKED_COURSES_1		EQU $a022
UNLOCKED_COURSES_1_COURSE_6		EQU %00000010 ;b1=Course 6
UNLOCKED_COURSES_1_COURSE_7		EQU %00000001 ;b0=Course 7
_SRAM_UNLOCKED_COURSES_2		EQU $a023
UNLOCKED_COURSES_2_MINIGAMES1	EQU %00000001 ;b0=extra minigame 1
UNLOCKED_COURSES_2_MINIGAMES2	EQU %00000010 ;b1=extra minigame 2
UNLOCKED_COURSES_2_MINIGAMES3	EQU %00000100 ;b2=extra minigame 3
UNLOCKED_COURSES_2_MINIGAMES4	EQU %00001000 ;b3=extra minigame 4
UNLOCKED_COURSES_2_COURSE_S1	EQU %01000000 ;b6=course S1 (can be unlocked without online)
UNLOCKED_COURSES_2_COURSE_S2	EQU %10000000 ;b7=course S2 (can be unlocked without online)

_RAM_CURRENT_SLOT_COMPLETED_TOURNAMENTS		EQU $c8ce
COMPLETED_TOURNAMENTS_STATES				EQU %10000000
COMPLETED_TOURNAMENTS_CARIBBEAN				EQU %01000000
COMPLETED_TOURNAMENTS_PYRAMID				EQU %00100000
COMPLETED_TOURNAMENTS_BRITTEN				EQU %00010000
COMPLETED_TOURNAMENTS_JIPANGU				EQU %00001000
COMPLETED_TOURNAMENTS_ATLANTIC				EQU %00000100
COMPLETED_TOURNAMENTS_PACIFIC				EQU %00000010

_RAM_CURRENT_SLOT_UNLOCKED_CLUBS_1 EQU $c8d4
UNLOCKED_CLUBS_1_ALL EQU %00011111 ;b0=CONTROL IRON, b1=CONTROL WOOD, b2=LONG DRIVE WEDGE, b3=LONG DRIVE IRON, b4=LONG DRIVE WOOD
_RAM_CURRENT_SLOT_UNLOCKED_CLUBS_2 EQU $c8d5
UNLOCKED_CLUBS_2_ALL EQU %01110011 ;b0=LOW-BALL WOOD, b1=BACKSPIN WEDGE, b4=HANDLING WEDGE, b5=HANDLING IRON, b6=HANDLING WOOD
_RAM_CURRENT_SLOT_UNLOCKED_CLUBS_3 EQU $c8d6
UNLOCKED_CLUBS_3_ALL EQU %11111111 ;b0=STRAIGHT WEDGE, b1=STRAIGHT IRON, b2=STRAIGHT WOOD, b3=MEAT WEDGE, b4=MEAT IRON, b5=MEAT WOOD, b6=LOW-BALL WEDGE, b7=LOW-BALL IRON
_RAM_CURRENT_SLOT_UNLOCKED_CLUBS_4 EQU $c8d7
UNLOCKED_CLUBS_4_ALL EQU %11001100 ;b2=LOW SPIN WEDGE, b3=LOW SPIN IRON, b6=BIG DRIVE IRON, b7=BIG DRIVE WOOD
_RAM_CURRENT_SLOT_UNLOCKED_CLUBS_5 EQU $c8d8
UNLOCKED_CLUBS_5_ALL EQU %11000000 ;b6=GAMBLING WOOD, b7=HYPERSPIN WEDGE

_RAM_CURRENT_SLOT_MOBILE_POINTS EQU $ca3e



SECTION "Bank 3 - Save to SRAM", ROMX[$4c04],BANK[$03]
save_sram:
	;call	$40f6
	call	hook_unlock_mobile_content

SECTION "Bank 0 - Free space", ROM0[$3fc4]
_UNLOCKED_CLUBS_BYTES:
	DB UNLOCKED_CLUBS_1_ALL
	DB UNLOCKED_CLUBS_2_ALL
	DB UNLOCKED_CLUBS_3_ALL
	DB UNLOCKED_CLUBS_4_ALL
	DB UNLOCKED_CLUBS_5_ALL
	DB UNLOCKED_COURSES_1_COURSE_6 + UNLOCKED_COURSES_1_COURSE_7
	DB UNLOCKED_COURSES_2_MINIGAMES1 + UNLOCKED_COURSES_2_MINIGAMES2 +UNLOCKED_COURSES_2_MINIGAMES3 +UNLOCKED_COURSES_2_MINIGAMES4

	
hook_unlock_mobile_content:
	push	hl
	push	de
	push	bc

	;unlock clubs
	ld		b, COMPLETED_TOURNAMENTS_STATES
	ld		hl, _UNLOCKED_CLUBS_BYTES
	ld		de, _RAM_CURRENT_SLOT_UNLOCKED_CLUBS_1

	;check if TOURNAMENTS are completed
.unlock_mobile_content_loop:
	ld		a, [_RAM_CURRENT_SLOT_COMPLETED_TOURNAMENTS]
	and		b
	jr		z, .finish

	ld		a, [de]
	or		[hl]
	ld		[de], a

	inc		l
	inc		e

	srl		b
	bit		2, b
	jr		z, .unlock_mobile_content_loop

	;unlock courses & extra minigames (if Jipangu tournament is completed)
	ld		de, _SRAM_UNLOCKED_COURSES_1
	
	ld		a, [de]
	or		[hl]
	ld		[de], a

	inc		l ;hl=_SRAM_UNLOCKED_COURSES_2
	inc		e

	ld		a, [de]
	or		[hl]
	ld		[de], a

.finish:
	pop		bc
	pop		de
	pop		hl

	jp		$40f6













;UNLOCK MOBILE CHARACTERS
_SRAM_UNLOCKED_CHARACTERS_NORMAL EQU $a020 ;lower bits: Bean, Rozary, Powert and Bird
_SRAM_UNLOCKED_CHARACTERS_MOBILE EQU $a021 ;higher bits: Mario series, lower bits: Mario Golf avatars


SECTION "Bank 3 - Unlock character", ROMX[$4f39],BANK[$03]
;call $49cc
call unlock_mario_characters

SECTION "Bank 3 - Free space", ROMX[$7ff5],BANK[$03]
unlock_mario_characters:
	bit		0, l
	jr		nz, .no_a020 ;skip if unlocking a Mario Golf character (hl=$a021)

	;now we unlock Mario characters
	inc		l
	or		[hl]
	ld		[hl], a
	dec		l

.no_a020:
	jp $49cc
ENDC